apply plugin: 'groovy'


project.ext {
    VERSION="0.0.1"
    STAGE="build/stage/groovy-ngs-utils-$VERSION"
    // GROOVY_VERSION="2.4.13"
}


/*
 TODO: use proguard to shrink down the output jars
 */
/*

buildscript {
    repositories {
        flatDir dirs: '/Users/simon/work/proguard/proguard4.9/lib'
    }
    dependencies {
        classpath ':proguard'
    }
}
*/

repositories {
    mavenCentral()
    maven { 
        name "biojava"
        url "https://www.biojava.org/download/maven/" 
    }
    maven { url 'https://jitpack.io' }
}

configurations {
  compile
}

dependencies {
     compile group: 'org.codehaus.groovy', name: 'groovy', version: "$GROOVY_VERSION"
     compile group: 'org.codehaus.groovy', name: 'groovy-xml', version: "$GROOVY_VERSION"
     compile group: 'org.codehaus.groovy', name: 'groovy-ant', version: "$GROOVY_VERSION"
     compile group: 'org.codehaus.groovy', name: 'groovy-json', version: "$GROOVY_VERSION"
     compile group: 'org.codehaus.groovy', name: 'groovy-sql', version: "$GROOVY_VERSION"
     if(!GROOVY_VERSION.startsWith('2.4')) {
         compile group: 'org.codehaus.groovy', name: 'groovy-cli-commons', version: "$GROOVY_VERSION"
     }
     compile group: 'org.codehaus.gpars', name: 'gpars', version: '1.2.1'

     compile group: 'org.apache.ivy', name:'ivy', version:'2.2.0' 
     compile group: 'org.biojava', name: 'biojava-core', version: '4.2.1'
     compile group: 'org.biojava', name:'biojava-alignment', version:'4.2.1' 
         
     compile 'com.lowagie:itext:2.1.7'
     compile group: 'org.apache.commons', name: 'commons-compress', version: '1.4.1'
     compile group: 'commons-io', name: 'commons-io', version: '2.4'
     compile 'org.apache.commons:commons-math3:3.0'
     compile group: 'com.google.guava', name: 'guava', version: '26.0-jre'
     compile group: 'com.github.scribejava', name: 'scribejava-core', version: '5.6.0'
     compile group: 'org.yaml', name: 'snakeyaml', version: '2.0'
     compile group: 'commons-cli', name: 'commons-cli', version: '1.4'
     compile group: 'commons-cli', name: 'commons-cli', version: '1.4'

     if(GROOVY_VERSION.startsWith("3.")) {
	 compile 'javax.xml.bind:jaxb-api:2.3.0'
	 compile 'com.sun.xml.bind:jaxb-core:2.3.0.1'
	 compile 'com.sun.xml.bind:jaxb-impl:2.3.0.1'
	 compile 'javax.activation:activation:1.1.1'
     }

     compile group:'org.slf4j', name:'slf4j-jdk14', version: '1.7.5'
     
     // compile group: 'com.github.samtools', name: 'htsjdk', version: '1.141'
     // compile 'org.utgenome.thirdparty:picard:1.102.0'
     compile 'com.xlson.groovycsv:groovycsv:1.0'
     // compile group: 'de.erichseifert.gral', name: 'gral-core', version: '0.11'
     //compile 'log4j:log4j:1.2.17'
     compile group: 'org.xerial', name: 'sqlite-jdbc', version: '3.36.0.3'

     compile files(fileTree(dir:'lib', includes:['*.jar']))
     compile project('htsjdk')
     compile project('graxxia')

     if(GROOVY_VERSION.startsWith('3.'))
        testCompile('org.spockframework:spock-core:2.3-groovy-3.0')
     else
        testCompile('org.spockframework:spock-core:1.3-groovy-2.5')
}

// Bundle all dependencies into output library
jar {
    from configurations.compile.grep { 
        !it.name.startsWith("cramtools") && 
        !it.name.contains('log4j') &&
        !it.name.startsWith('groovy-') &&
        !it.name.contains('-linux-armhf-') &&
        !it.name.contains('win-i686') &&
        !it.name.contains('linux-i686')
    }.collect { 

        // println "Including $it"; 


        (it.isDirectory() || it.name.endsWith('.pom')) ? it : zipTree(it) 
    }

    /*
    from configurations.compile.grep { it.name.startsWith("cramtools") }.collect { 
        // println "Including $it"; 
        it.isDirectory() ? it : zipTree(it) 
    }
    */

    exclude { details ->

        if(details.relativePath.pathString.startsWith("groovy/stream")) {
            return false
        }
        if(details.relativePath.pathString.startsWith("groovy/"))
            return true

        if(details.relativePath.pathString.contains("groovyjarjar"))
            return true

        if(details.relativePath.pathString ==~ '.*/Log4J.*.class$')
            return true

        if(details.relativePath.pathString.contains("log4j"))
            return true

        if(details.relativePath.pathString.contains("commons/logging"))
            return true

        if(details.name.endsWith('.pom'))
            return true

        return false
    }

    exclude "org/codehaus/**"
    exclude "META-INF/groovy*"
    exclude "META-INF/**/*groovy*"
    exclude "META-INF/BCKEY*"
    exclude "org/bouncycastle/**"
    exclude "org/codehaus/mojo"
    exclude "org/slf4j/impl/**"
}

groovydoc {
    destinationDir = new File('doc').absoluteFile
    source = 'src/main/groovy'
    overviewText = resources.text.fromFile('src/main/groovy/overview.html')
    docTitle = ''
    doLast {
        println "Doing it now"
        ant.copy file:"src/main/resources/docstyle.css", tofile:'doc/stylesheet.css', overwrite:true
    }
}

// println "The gradle version is: " + project.gradle.gradleVersion

compileGroovy {
    if(project.gradle.gradleVersion.tokenize('.')[0].toInteger() > 5 )
        groovyOptions.parameters = true
}

test {
    filter {
     includeTestsMatching "gngs.*"
    }

/* Uncomment to see test output
    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }
*/
}


if (rootProject == project) {
    wrapper {
        gradleVersion = '5.2.1'
    }
}

